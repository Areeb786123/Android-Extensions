apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: "${project.rootDir}/version.gradle"

android {
    compileSdkVersion parent.ext.compileSdkVersion
    buildToolsVersion parent.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion parent.ext.minSdkVersion
        targetSdkVersion parent.ext.targetSdkVersion

        consumerProguardFiles 'proguard-rules.pro'

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        // Reduce META-INF file conflicts
        kotlinOptions.freeCompilerArgs += ['-module-name', "${parent.ext.groupId}.${project.getName()}"]
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.getName()}-${project.version}.aar"
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            lib(MavenPublication) {
                from components.release

                version = project.version
                groupId parent.ext.groupId // from parent root.gradle ext
                artifactId project.getName()

                // Add sources too
                afterEvaluate {
                    artifact(sourcesJar) {
                        classifier = 'sources'
                        extension = 'jar'
                    }

                    pom {
                        name = project.getName()
                        description = "${name} Android extensions"
                        url = 'https://github.com/tunjid/Android-Extensions'
                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'https://github.com/tunjid/tunjid/Android-Extensions/develop/license.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'tunjid'
                                name = 'Adetunji Dahunsi'
                                email = 'tjdah100@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:github.com/tunjid/Android-Extensions.git'
                            developerConnection = 'scm:git:ssh://github.com/tunjid/Android-Extensions.git'
                            url = 'https://github.com/tunjid/Android-Extensions/tree/main'
                        }
                    }
                }
            }
        }
        repositories {
            parent.ext.publishRepositories.each { repo ->
                def props = (repo as ConfigObject).toProperties()
                maven {
                    name = props['name']
                    url = props['url']
                    credentials {
                        username = props['credentials.username']
                        password = props['credentials.password']
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications
    }
}

// Gradle task to generate sources after building a release aar
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn 'sourcesJar'
    }
}